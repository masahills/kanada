plugins {
    id 'java-library'
    id 'maven-publish'
}

group = 'com.iciao'
version = '1.0.0'

java {
    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11
}

repositories {
    mavenCentral()
}

dependencies {
    testImplementation 'junit:junit:4.13.2'
}

task generateMapping(type: Exec) {
    description 'Generate KanaMapping.java from kanatable.tsv'
    commandLine 'python3', 'tools/generate-mapping.py'
    inputs.file 'tools/mappings/kanatable.tsv'
    outputs.file 'src/main/java/com/iciao/kanada/maps/KanaMappingData.java'
}

compileJava.dependsOn generateMapping

task reallyClean(type: Delete) {
    description 'Clean build artifacts and regenerate dictionary files'
    delete layout.buildDirectory.get().asFile
    delete 'dictionary/japanese/kanwadict.dat'
    delete 'src/main/java/com/iciao/kanada/maps/KanaMappingData.java'
}

reallyClean.dependsOn clean

jar {
    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}

publishing {
    publications {
        maven(MavenPublication) {
            from components.java
        }
    }
}